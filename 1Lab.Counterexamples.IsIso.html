<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="en" xml:lang="en">
<head>
  <meta charset="utf-8" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0" />
  <meta name="keywords" content="Agda,categories,topos" />

  <title>…ab.Counterexamples.IsIso - 1Lab</title>

  <link rel="stylesheet" href="/css/default.css" />
  <link rel="stylesheet" href="/css/katex.min.css" />

  <meta name="twitter:card" content="summary" />
  <meta name="twitter:title" content="…ab.Counterexamples.IsIso - 1Lab" />
  <meta name="twitter:image" content="https://1lab.dev/static/cube-128x.png" />

  <meta name="og:title" content="…ab.Counterexamples.IsIso - 1Lab" />
  <meta name="og:image" content="https://1lab.dev/static/cube-128x.png" />
  <meta name="og:site-name" content="1Lab" />
  <meta name="og:type" content="website" />

    <meta name="og:description" content="&lt;p&gt;By counter example we show that is-iso is not a proposition.&lt;/p&gt;" />
  <meta name="twitter:description" content="&lt;p&gt;By counter example we show that is-iso is not a proposition.&lt;/p&gt;" />
  <meta name="description" content="&lt;p&gt;By counter example we show that is-iso is not a proposition.&lt;/p&gt;" />
  
  <script defer src="/main.js"></script>

  <noscript>
    <style>
      body span.reasoning-step .as-written {
        display: inline;
      }

      body span.reasoning-step .alternate {
        display: none;
      }
    </style>
  </noscript>

  <!-- Google font embeds -->
  <link rel="preconnect" href="https://fonts.googleapis.com">
  <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
  <link href="https://fonts.googleapis.com/css2?family=EB+Garamond:ital,wght@0,400;0,500;0,600;0,700;0,800;1,400;1,500;1,600;1,700;1,800&amp;family=Inria+Sans:ital,wght@0,300;0,400;0,700;1,300;1,400;1,700&amp;display=swap" rel="stylesheet">
</head>

<body class="text-page">
<main>
<div id="post-toc-container" style="padding-left: 1em; padding-right: 1em;">

  <!-- Table of contents (only if the page has a table of contents) -->
    <aside id="toc">
    <div id="toc-container" style="min-width: 10em; font-size: 15pt;">
      <!-- Title for the page -->
      <h3 class="Agda" style="margin-top: 0; margin-bottom: 0; white-space: pre;">
                <a class="Module">…ab.Counterexamples.IsIso</a>
              </h3>

      <!-- Cube logo -->
      <a id="logo" href="/">
        <img alt="1Lab" src="/static/cube-72x.png" style="display: block; margin-bottom: 1em; margin: auto;" width="32px" height="32px" />
      </a>

      <!-- Actual table of contents (separated from the rest by
      horizontal rules) -->
      <hr />
      <ul>
      <li><a href="#is-iso-is-not-a-proposition"><a href="#is-iso-is-not-a-proposition" class="header-link">is-iso is not a proposition<span class="header-link-emoji">🔗</span></a></a></li>
      </ul>
      <hr />

      <!-- Sidebar controls -->
      <div style="display: flex; gap: 0.25em; flex-flow: row wrap;">
        <!-- Sidebar equations control -->
        <span class="equations" style="display: flex; align-items: center; gap: 0.25em; flex-wrap: nowrap;">
          <input name="sidebar-eqns" type="checkbox" class="equations" id="sidebar-eqns">
          <label for="sidebar-eqns">Equations</label>
        </span>

        <!-- Sidebar fonts control -->
        <span style="display: flex; align-items: center; gap: 0.25em; flex-wrap: nowrap;">
          <input name="sidebar-font" type="checkbox" id="toggle-fonts">
          <label for="toggle-fonts">Serif Font</label>
        </span>

        <!-- Sidebar footnotes control -->
        <span class="inline-footnotes" style="display: none; align-items: center; gap: 0.25em; flex-wrap: nowrap;">
          <input name="sidebar-fns" type="checkbox" class="inline-footnotes" id="sidebar-fns">
          <label for="sidebar-fns">Inline Footnotes</label>
        </span>
      </div>

      <hr />

      <!-- Navbar navigation (lol) -->
      <div id="return" style="white-space: nowrap;">
        <div class="search-form search-form-proxy" role="form">
          <input id="search-box-proxy" type_="text" placeholder="Search..." autocomplete="off" tabindex="0"></input>
        </div>

              <a href="/">back to index</a> <br />
              <a href="all-pages.html">view all pages</a> <br />
        <a href="https://github.com/plt-amy/cubical-1lab/blob/4e67ef9186153801b52ce33e3bfd1254cbb7dd94/src/1Lab/Counterexamples/IsIso.lagda.md">link to source</a> <br />
                <hr /> <div class="author-list">Written by Amélia Liao and Jappie Klooster</div> <br />
              </div>
    </div>
  </aside>
  
  <article>
    <!-- Article navigation (narrow screens only) -->
    <div class="narrow-only" id="article-nav">
      <!-- Index return anchor (only if the page is not the index) -->
            <div id="return">
        <a href="/">back to index</a>
      </div>
      
      <!-- Navbar equations control -->
      <span class="equations" style="display: flex; gap: 0.25em; flex-wrap: nowrap;">
        <input name="navbar-eqns" type="checkbox" class="equations" id="navbar-eqns">
        <label for="navbar-eqns">Equations</label>
      </span>

      <!-- Navbar footnotes control -->
      <span class="inline-footnotes" style="display: none; gap: 0.25em; flex-wrap: nowrap;">
        <input name="navbar-fns" type="checkbox" class="inline-footnotes" id="navbar-fns">
        <label for="navbar-fns">Inline Footnotes</label>
      </span>
    </div>

<pre class="Agda"><a id="97" class="Keyword">open</a> <a id="102" class="Keyword">import</a> <a id="109" href="1Lab.HLevel.Retracts.html" class="Module">1Lab.HLevel.Retracts</a>
<a id="130" class="Keyword">open</a> <a id="135" class="Keyword">import</a> <a id="142" href="1Lab.Path.Groupoid.html" class="Module">1Lab.Path.Groupoid</a>
<a id="161" class="Keyword">open</a> <a id="166" class="Keyword">import</a> <a id="173" href="1Lab.Univalence.html" class="Module">1Lab.Univalence</a>
<a id="189" class="Keyword">open</a> <a id="194" class="Keyword">import</a> <a id="201" href="1Lab.HIT.S1.html" class="Module">1Lab.HIT.S1</a>
<a id="213" class="Keyword">open</a> <a id="218" class="Keyword">import</a> <a id="225" href="1Lab.HLevel.html" class="Module">1Lab.HLevel</a>
<a id="237" class="Keyword">open</a> <a id="242" class="Keyword">import</a> <a id="249" href="1Lab.Equiv.html" class="Module">1Lab.Equiv</a>
<a id="260" class="Keyword">open</a> <a id="265" class="Keyword">import</a> <a id="272" href="1Lab.Path.html" class="Module">1Lab.Path</a>
<a id="282" class="Keyword">open</a> <a id="287" class="Keyword">import</a> <a id="294" href="1Lab.Type.html" class="Module">1Lab.Type</a>

<a id="305" class="Keyword">module</a> <a id="312" href="1Lab.Counterexamples.IsIso.html" class="Module">1Lab.Counterexamples.IsIso</a> <a id="339" class="Keyword">where</a>
</pre>
<h1 id="is-iso-is-not-a-proposition"><a href="#is-iso-is-not-a-proposition" class="header-link">is-iso is not a proposition<span class="header-link-emoji">🔗</span></a></h1>
<p>We show that if <span class="Agda"><a href="1Lab.Equiv.html#6026" class="Record">is-iso</a></span> were a <a href="1Lab.HLevel.html#2597">proposition</a>, then <code>(x : A) → x ≡ x</code> would be contractible for any choice of <code>A</code>. Taking <code>A</code> to be <span class="Agda"><a href="1Lab.HIT.S1.html#622" class="Datatype">S¹</a></span>, we show that this can not be the case. Suppose that is-iso is a proposition.</p>
<pre class="Agda"><a id="662" class="Keyword">module</a>
  <a id="671" href="1Lab.Counterexamples.IsIso.html#671" class="Module">_</a> <a id="673" class="Symbol">(</a><a id="674" href="1Lab.Counterexamples.IsIso.html#674" class="Bound">iso-is-prop</a> <a id="686" class="Symbol">:</a> <a id="688" class="Symbol">∀</a> <a id="690" class="Symbol">{</a><a id="691" href="1Lab.Counterexamples.IsIso.html#691" class="Bound">ℓ</a><a id="692" class="Symbol">}</a> <a id="694" class="Symbol">{</a><a id="695" href="1Lab.Counterexamples.IsIso.html#695" class="Bound">A</a> <a id="697" href="1Lab.Counterexamples.IsIso.html#697" class="Bound">B</a> <a id="699" class="Symbol">:</a> <a id="701" href="1Lab.Type.html#391" class="Primitive">Type</a> <a id="706" href="1Lab.Counterexamples.IsIso.html#691" class="Bound">ℓ</a><a id="707" class="Symbol">}</a> <a id="709" class="Symbol">{</a><a id="710" href="1Lab.Counterexamples.IsIso.html#710" class="Bound">f</a> <a id="712" class="Symbol">:</a> <a id="714" href="1Lab.Counterexamples.IsIso.html#695" class="Bound">A</a> <a id="716" class="Symbol">→</a> <a id="718" href="1Lab.Counterexamples.IsIso.html#697" class="Bound">B</a><a id="719" class="Symbol">}</a> <a id="721" class="Symbol">→</a> <a id="723" href="1Lab.HLevel.html#2597" data-type="Type ℓ → Type ℓ" class="Function">is-prop</a> <a id="731" class="Symbol">(</a><a id="732" href="1Lab.Equiv.html#6026" data-type="(f : A → B₁) → Type (A.ℓ₁ ⊔ B.ℓ₁)" class="Record">is-iso</a> <a id="739" href="1Lab.Counterexamples.IsIso.html#710" class="Bound">f</a><a id="740" class="Symbol">))</a>
  <a id="745" class="Keyword">where</a>
</pre>
<p>First we characterise the type <span class="Agda"><a href="1Lab.Equiv.html#6026" class="Record">is-iso f</a></span> by showing that, if it is inhabited, then it is equivalent to the <em>centre</em> of <code>A</code>, i.e. the loop-assigning maps of <code>A</code>:</p>
<pre class="Agda">  <a id="949" href="1Lab.Counterexamples.IsIso.html#949" data-type="is-prop (is-iso f) → is-iso f → is-iso f ≃ ((x : A) → x ≡ x)" class="Function">lemma</a> <a id="955" class="Symbol">:</a> <a id="957" class="Symbol">∀</a> <a id="959" class="Symbol">{</a><a id="960" href="1Lab.Counterexamples.IsIso.html#960" class="Bound">ℓ</a><a id="961" class="Symbol">}</a> <a id="963" class="Symbol">{</a><a id="964" href="1Lab.Counterexamples.IsIso.html#964" class="Bound">A</a> <a id="966" href="1Lab.Counterexamples.IsIso.html#966" class="Bound">B</a> <a id="968" class="Symbol">:</a> <a id="970" href="1Lab.Type.html#391" class="Primitive">Type</a> <a id="975" href="1Lab.Counterexamples.IsIso.html#960" class="Bound">ℓ</a><a id="976" class="Symbol">}</a> <a id="978" class="Symbol">{</a><a id="979" href="1Lab.Counterexamples.IsIso.html#979" class="Bound">f</a> <a id="981" class="Symbol">:</a> <a id="983" href="1Lab.Counterexamples.IsIso.html#964" class="Bound">A</a> <a id="985" class="Symbol">→</a> <a id="987" href="1Lab.Counterexamples.IsIso.html#966" class="Bound">B</a><a id="988" class="Symbol">}</a> <a id="990" class="Symbol">→</a> <a id="992" href="1Lab.Equiv.html#6026" data-type="(f : A → B₁) → Type (A.ℓ₁ ⊔ B.ℓ₁)" class="Record">is-iso</a> <a id="999" href="1Lab.Counterexamples.IsIso.html#979" class="Bound">f</a> <a id="1001" class="Symbol">→</a> <a id="1003" href="1Lab.Equiv.html#6026" data-type="(f : A → B₁) → Type (A.ℓ₁ ⊔ B.ℓ₁)" class="Record">is-iso</a> <a id="1010" href="1Lab.Counterexamples.IsIso.html#979" class="Bound">f</a> <a id="1012" href="1Lab.Equiv.html#2439" data-type="Type ℓ₁ → Type ℓ₂ → Type (ℓ₁ ⊔ ℓ₂)" class="Function Operator">≃</a> <a id="1014" class="Symbol">((</a><a id="1016" href="1Lab.Counterexamples.IsIso.html#1016" class="Bound">x</a> <a id="1018" class="Symbol">:</a> <a id="1020" href="1Lab.Counterexamples.IsIso.html#964" class="Bound">A</a><a id="1021" class="Symbol">)</a> <a id="1023" class="Symbol">→</a> <a id="1025" href="1Lab.Counterexamples.IsIso.html#1016" class="Bound">x</a> <a id="1027" href="Agda.Builtin.Cubical.Path.html#381" data-type="A → A → Type ℓ" class="Function Operator">≡</a> <a id="1029" href="1Lab.Counterexamples.IsIso.html#1016" class="Bound">x</a><a id="1030" class="Symbol">)</a>
  <a id="1034" href="1Lab.Counterexamples.IsIso.html#949" data-type="is-prop (is-iso f) → is-iso f → is-iso f ≃ ((x : A) → x ≡ x)" class="Function">lemma</a> <a id="1040" class="Symbol">{</a><a id="1041" class="Argument">A</a> <a id="1043" class="Symbol">=</a> <a id="1045" href="1Lab.Counterexamples.IsIso.html#1045" class="Bound">A</a><a id="1046" class="Symbol">}</a> <a id="1048" class="Symbol">{</a><a id="1049" href="1Lab.Counterexamples.IsIso.html#1049" class="Bound">B</a><a id="1050" class="Symbol">}</a> <a id="1052" class="Symbol">{</a><a id="1053" href="1Lab.Counterexamples.IsIso.html#1053" class="Bound">f</a><a id="1054" class="Symbol">}</a> <a id="1056" href="1Lab.Counterexamples.IsIso.html#1056" class="Bound">iiso</a> <a id="1061" class="Symbol">=</a>
    <a id="1067" href="1Lab.Univalence.html#19821" data-type="(P : (B₁ : Type ℓ) → A ≃ B₁ → Type ℓ&#39;) →
P A ((λ x → x) , id-equiv) → (e : A ≃ B₁) → P B₁ e" class="Function">EquivJ</a> <a id="1074" class="Symbol">(λ</a> <a id="1077" href="1Lab.Counterexamples.IsIso.html#1077" class="Bound">_</a> <a id="1079" href="1Lab.Counterexamples.IsIso.html#1079" class="Bound">f</a> <a id="1081" class="Symbol">→</a> <a id="1083" href="1Lab.Equiv.html#6026" data-type="(f : A → B₁) → Type (A.ℓ₁ ⊔ B.ℓ₁)" class="Record">is-iso</a> <a id="1090" class="Symbol">(</a><a id="1091" href="1Lab.Counterexamples.IsIso.html#1079" class="Bound">f</a> <a id="1093" class="Symbol">.</a><a id="1094" href="Agda.Builtin.Sigma.html#252" data-type="∑ A B₁ → A" class="Field">fst</a><a id="1097" class="Symbol">)</a> <a id="1099" href="1Lab.Equiv.html#2439" data-type="Type ℓ₁ → Type ℓ₂ → Type (ℓ₁ ⊔ ℓ₂)" class="Function Operator">≃</a> <a id="1101" class="Symbol">((</a><a id="1103" href="1Lab.Counterexamples.IsIso.html#1103" class="Bound">x</a> <a id="1105" class="Symbol">:</a> <a id="1107" href="1Lab.Counterexamples.IsIso.html#1045" class="Bound">A</a><a id="1108" class="Symbol">)</a> <a id="1110" class="Symbol">→</a> <a id="1112" href="1Lab.Counterexamples.IsIso.html#1103" class="Bound">x</a> <a id="1114" href="Agda.Builtin.Cubical.Path.html#381" data-type="A → A → Type ℓ" class="Function Operator">≡</a> <a id="1116" href="1Lab.Counterexamples.IsIso.html#1103" class="Bound">x</a><a id="1117" class="Symbol">))</a>
           <a id="1131" class="Symbol">(</a><a id="1132" href="1Lab.Equiv.html#20161" data-type="Iso A B₁ → A ≃ B₁" class="Function">Iso→Equiv</a> <a id="1142" href="1Lab.Counterexamples.IsIso.html#1507" data-type="(iso-is-prop : is-prop (is-iso f)) (iiso : is-iso f) →
Iso (is-iso (λ x → x)) ((x : A) → x ≡ x)" class="Function">helper</a><a id="1148" class="Symbol">)</a>
           <a id="1161" class="Symbol">(</a><a id="1162" href="1Lab.Counterexamples.IsIso.html#1053" class="Bound">f</a> <a id="1164" href="Agda.Builtin.Sigma.html#236" data-type="(fst₁ : A) (snd₁ : B₁ fst₁) → ∑ A B₁" class="InductiveConstructor Operator">,</a> <a id="1166" href="1Lab.Equiv.html#19815" data-type="(i : is-iso f) → is-equiv f" class="Function">is-iso→is-equiv</a> <a id="1182" href="1Lab.Counterexamples.IsIso.html#1056" class="Bound">iiso</a><a id="1186" class="Symbol">)</a>
    <a id="1192" class="Keyword">where</a>
</pre>
<p>By <a href="1Lab.Univalence.html#consequences">equivalence induction</a>, it suffices to cover the case where <code>f</code> is the identity function. In that case, we can construct an isomorphism quite readily, where the proof uses our assumption <span class="Agda"><a href="1Lab.Counterexamples.IsIso.html#674" class="Bound">iso-is-prop</a></span> for convenience.</p>
<pre class="Agda">      <a id="1507" href="1Lab.Counterexamples.IsIso.html#1507" data-type="(iso-is-prop : is-prop (is-iso f)) (iiso : is-iso f) →
Iso (is-iso (λ x → x)) ((x : A) → x ≡ x)" class="Function">helper</a> <a id="1514" class="Symbol">:</a> <a id="1516" href="1Lab.Equiv.html#6298" data-type="Type ℓ₁ → Type ℓ₂ → Type (ℓ₁ ⊔ ℓ₂)" class="Function">Iso</a> <a id="1520" class="Symbol">_</a> <a id="1522" class="Symbol">_</a>
      <a id="1530" href="1Lab.Counterexamples.IsIso.html#1507" data-type="(iso-is-prop : is-prop (is-iso f)) (iiso : is-iso f) →
Iso (is-iso (λ x → x)) ((x : A) → x ≡ x)" class="Function">helper</a> <a id="1537" class="Symbol">.</a><a id="1538" href="Agda.Builtin.Sigma.html#252" data-type="∑ A B₁ → A" class="Field">fst</a> <a id="1542" href="1Lab.Counterexamples.IsIso.html#1542" class="Bound">iiso</a> <a id="1547" href="1Lab.Counterexamples.IsIso.html#1547" class="Bound">x</a> <a id="1549" class="Symbol">=</a>
        <a id="1559" href="1Lab.Path.html#10558" data-type="x ≡ y → y ≡ x" class="Function">sym</a> <a id="1563" class="Symbol">(</a><a id="1564" href="1Lab.Counterexamples.IsIso.html#1542" class="Bound">iiso</a> <a id="1569" class="Symbol">.</a><a id="1570" href="1Lab.Equiv.html#6182" data-type="(r : is-iso f) → is-left-inverse (r .inv) f" class="Field">is-iso.linv</a> <a id="1582" href="1Lab.Counterexamples.IsIso.html#1547" class="Bound">x</a><a id="1583" class="Symbol">)</a> <a id="1585" href="1Lab.Path.html#39472" data-type="x ≡ y → y ≡ z → x ≡ z" class="Function Operator">∙</a> <a id="1587" href="1Lab.Counterexamples.IsIso.html#1542" class="Bound">iiso</a> <a id="1592" class="Symbol">.</a><a id="1593" href="1Lab.Equiv.html#6148" data-type="(r : is-iso f) → is-right-inverse (r .inv) f" class="Field">is-iso.rinv</a> <a id="1605" href="1Lab.Counterexamples.IsIso.html#1547" class="Bound">x</a>
      <a id="1613" href="1Lab.Counterexamples.IsIso.html#1507" data-type="(iso-is-prop : is-prop (is-iso f)) (iiso : is-iso f) →
Iso (is-iso (λ x → x)) ((x : A) → x ≡ x)" class="Function">helper</a> <a id="1620" class="Symbol">.</a><a id="1621" href="Agda.Builtin.Sigma.html#264" data-type="(r : ∑ A B₁) → B₁ (fst r)" class="Field">snd</a> <a id="1625" class="Symbol">.</a><a id="1626" href="1Lab.Equiv.html#6132" data-type="is-iso f → B₁ → A" class="Field">is-iso.inv</a> <a id="1637" href="1Lab.Counterexamples.IsIso.html#1637" class="Bound">x</a> <a id="1639" class="Symbol">=</a> <a id="1641" href="1Lab.Equiv.html#6116" data-type="(inv₁ : B₁ → A) (rinv₁ : is-right-inverse inv₁ f)
(linv₁ : is-left-inverse inv₁ f) →
is-iso f" class="InductiveConstructor">iso</a> <a id="1645" class="Symbol">(λ</a> <a id="1648" href="1Lab.Counterexamples.IsIso.html#1648" class="Bound">x</a> <a id="1650" class="Symbol">→</a> <a id="1652" href="1Lab.Counterexamples.IsIso.html#1648" class="Bound">x</a><a id="1653" class="Symbol">)</a> <a id="1655" href="1Lab.Counterexamples.IsIso.html#1637" class="Bound">x</a> <a id="1657" class="Symbol">(λ</a> <a id="1660" href="1Lab.Counterexamples.IsIso.html#1660" class="Bound">_</a> <a id="1662" class="Symbol">→</a> <a id="1664" href="1Lab.Path.html#3593" data-type="x ≡ x" class="Function">refl</a><a id="1668" class="Symbol">)</a>
      <a id="1676" href="1Lab.Counterexamples.IsIso.html#1507" data-type="(iso-is-prop : is-prop (is-iso f)) (iiso : is-iso f) →
Iso (is-iso (λ x → x)) ((x : A) → x ≡ x)" class="Function">helper</a> <a id="1683" class="Symbol">.</a><a id="1684" href="Agda.Builtin.Sigma.html#264" data-type="(r : ∑ A B₁) → B₁ (fst r)" class="Field">snd</a> <a id="1688" class="Symbol">.</a><a id="1689" href="1Lab.Equiv.html#6148" data-type="(r : is-iso f) → is-right-inverse (r .inv) f" class="Field">is-iso.rinv</a> <a id="1701" href="1Lab.Counterexamples.IsIso.html#1701" class="Bound">p</a> <a id="1703" class="Symbol">=</a> <a id="1705" href="1Lab.Path.html#58486" data-type="((x : A) → f x ≡ g x) → f ≡ g" class="Function">funext</a> <a id="1712" class="Symbol">λ</a> <a id="1714" href="1Lab.Counterexamples.IsIso.html#1714" class="Bound">x</a> <a id="1716" class="Symbol">→</a> <a id="1718" href="1Lab.Path.Groupoid.html#4623" data-type="(p : x ≡ y) → refl ∙ p ≡ p" class="Function">∙-id-l</a> <a id="1725" class="Symbol">_</a>
      <a id="1733" href="1Lab.Counterexamples.IsIso.html#1507" data-type="(iso-is-prop : is-prop (is-iso f)) (iiso : is-iso f) →
Iso (is-iso (λ x → x)) ((x : A) → x ≡ x)" class="Function">helper</a> <a id="1740" class="Symbol">.</a><a id="1741" href="Agda.Builtin.Sigma.html#264" data-type="(r : ∑ A B₁) → B₁ (fst r)" class="Field">snd</a> <a id="1745" class="Symbol">.</a><a id="1746" href="1Lab.Equiv.html#6182" data-type="(r : is-iso f) → is-left-inverse (r .inv) f" class="Field">is-iso.linv</a> <a id="1758" href="1Lab.Counterexamples.IsIso.html#1758" class="Bound">x</a> <a id="1760" class="Symbol">=</a> <a id="1762" href="1Lab.Counterexamples.IsIso.html#674" class="Bound">iso-is-prop</a> <a id="1774" class="Symbol">_</a> <a id="1776" class="Symbol">_</a>
</pre>
<p>We thus have that <code>is-iso id ≃ ((x : A) → x ≡ x)</code> - since the former is a prop (by assumption), then so is the latter:</p>
<pre class="Agda">  <a id="1913" href="1Lab.Counterexamples.IsIso.html#1913" data-type="is-prop (is-iso f) → is-prop ((x : A) → x ≡ x)" class="Function">is-prop-loops</a> <a id="1927" class="Symbol">:</a> <a id="1929" class="Symbol">∀</a> <a id="1931" class="Symbol">{</a><a id="1932" href="1Lab.Counterexamples.IsIso.html#1932" class="Bound">ℓ</a><a id="1933" class="Symbol">}</a> <a id="1935" class="Symbol">{</a><a id="1936" href="1Lab.Counterexamples.IsIso.html#1936" class="Bound">A</a> <a id="1938" class="Symbol">:</a> <a id="1940" href="1Lab.Type.html#391" class="Primitive">Type</a> <a id="1945" href="1Lab.Counterexamples.IsIso.html#1932" class="Bound">ℓ</a><a id="1946" class="Symbol">}</a> <a id="1948" class="Symbol">→</a> <a id="1950" href="1Lab.HLevel.html#2597" data-type="Type ℓ → Type ℓ" class="Function">is-prop</a> <a id="1958" class="Symbol">((</a><a id="1960" href="1Lab.Counterexamples.IsIso.html#1960" class="Bound">x</a> <a id="1962" class="Symbol">:</a> <a id="1964" href="1Lab.Counterexamples.IsIso.html#1936" class="Bound">A</a><a id="1965" class="Symbol">)</a> <a id="1967" class="Symbol">→</a> <a id="1969" href="1Lab.Counterexamples.IsIso.html#1960" class="Bound">x</a> <a id="1971" href="Agda.Builtin.Cubical.Path.html#381" data-type="A → A → Type ℓ" class="Function Operator">≡</a> <a id="1973" href="1Lab.Counterexamples.IsIso.html#1960" class="Bound">x</a><a id="1974" class="Symbol">)</a>
  <a id="1978" href="1Lab.Counterexamples.IsIso.html#1913" data-type="is-prop (is-iso f) → is-prop ((x : A) → x ≡ x)" class="Function">is-prop-loops</a> <a id="1992" class="Symbol">{</a><a id="1993" class="Argument">A</a> <a id="1995" class="Symbol">=</a> <a id="1997" href="1Lab.Counterexamples.IsIso.html#1997" class="Bound">A</a><a id="1998" class="Symbol">}</a> <a id="2000" class="Symbol">=</a> <a id="2002" href="1Lab.HLevel.Retracts.html#4068" data-type="(n : Nat) (f : A → B₁) →
is-equiv f → is-hlevel A n → is-hlevel B₁ n" class="Function">equiv→is-hlevel</a> <a id="2018" class="Number">1</a> <a id="2020" class="Symbol">(</a><a id="2021" href="1Lab.Counterexamples.IsIso.html#2070" data-type="(iso-is-prop : is-prop (is-iso f)) →
is-iso (λ x → x) ≃ ((x : A) → x ≡ x)" class="Function">helper</a> <a id="2028" class="Symbol">.</a><a id="2029" href="Agda.Builtin.Sigma.html#252" data-type="∑ A B₁ → A" class="Field">fst</a><a id="2032" class="Symbol">)</a> <a id="2034" class="Symbol">(</a><a id="2035" href="1Lab.Counterexamples.IsIso.html#2070" data-type="(iso-is-prop : is-prop (is-iso f)) →
is-iso (λ x → x) ≃ ((x : A) → x ≡ x)" class="Function">helper</a> <a id="2042" class="Symbol">.</a><a id="2043" href="Agda.Builtin.Sigma.html#264" data-type="(r : ∑ A B₁) → B₁ (fst r)" class="Field">snd</a><a id="2046" class="Symbol">)</a> <a id="2048" href="1Lab.Counterexamples.IsIso.html#674" class="Bound">iso-is-prop</a>
    <a id="2064" class="Keyword">where</a> <a id="2070" href="1Lab.Counterexamples.IsIso.html#2070" data-type="(iso-is-prop : is-prop (is-iso f)) →
is-iso (λ x → x) ≃ ((x : A) → x ≡ x)" class="Function">helper</a> <a id="2077" class="Symbol">=</a> <a id="2079" href="1Lab.Counterexamples.IsIso.html#949" data-type="is-prop (is-iso f) → is-iso f → is-iso f ≃ ((x : A) → x ≡ x)" class="Function">lemma</a> <a id="2085" class="Symbol">{</a><a id="2086" class="Argument">f</a> <a id="2088" class="Symbol">=</a> <a id="2090" class="Symbol">λ</a> <a id="2092" class="Symbol">(</a><a id="2093" href="1Lab.Counterexamples.IsIso.html#2093" class="Bound">x</a> <a id="2095" class="Symbol">:</a> <a id="2097" href="1Lab.Counterexamples.IsIso.html#1997" class="Bound">A</a><a id="2098" class="Symbol">)</a> <a id="2100" class="Symbol">→</a> <a id="2102" href="1Lab.Counterexamples.IsIso.html#2093" class="Bound">x</a><a id="2103" class="Symbol">}</a>
                     <a id="2126" class="Symbol">(</a><a id="2127" href="1Lab.Equiv.html#6116" data-type="(inv₁ : B₁ → A) (rinv₁ : is-right-inverse inv₁ f)
(linv₁ : is-left-inverse inv₁ f) →
is-iso f" class="InductiveConstructor">iso</a> <a id="2131" class="Symbol">(λ</a> <a id="2134" href="1Lab.Counterexamples.IsIso.html#2134" class="Bound">x</a> <a id="2136" class="Symbol">→</a> <a id="2138" href="1Lab.Counterexamples.IsIso.html#2134" class="Bound">x</a><a id="2139" class="Symbol">)</a> <a id="2141" class="Symbol">(λ</a> <a id="2144" href="1Lab.Counterexamples.IsIso.html#2144" class="Bound">x</a> <a id="2146" class="Symbol">→</a> <a id="2148" href="1Lab.Path.html#3593" data-type="x ≡ x" class="Function">refl</a><a id="2152" class="Symbol">)</a> <a id="2154" class="Symbol">(λ</a> <a id="2157" href="1Lab.Counterexamples.IsIso.html#2157" class="Bound">x</a> <a id="2159" class="Symbol">→</a> <a id="2161" href="1Lab.Path.html#3593" data-type="x ≡ x" class="Function">refl</a><a id="2165" class="Symbol">))</a>
</pre>
<p>Thus, it suffices to choose a type for which <code>(x : A) → x ≡ x</code> has two distinct elements. We go with the circle, <span class="Agda"><a href="1Lab.HIT.S1.html#622" class="Datatype">S¹</a></span>:</p>
<pre class="Agda">  <a id="2310" href="1Lab.Counterexamples.IsIso.html#2310" data-type="(iso-is-prop : is-prop (is-iso f)) → is-prop ((x : S¹) → x ≡ x) → ⊥" class="Function">¬is-prop-loops</a> <a id="2325" class="Symbol">:</a> <a id="2327" href="1Lab.HLevel.html#2597" data-type="Type ℓ → Type ℓ" class="Function">is-prop</a> <a id="2335" class="Symbol">((</a><a id="2337" href="1Lab.Counterexamples.IsIso.html#2337" class="Bound">x</a> <a id="2339" class="Symbol">:</a> <a id="2341" href="1Lab.HIT.S1.html#622" data-type="Type" class="Datatype">S¹</a><a id="2343" class="Symbol">)</a> <a id="2345" class="Symbol">→</a> <a id="2347" href="1Lab.Counterexamples.IsIso.html#2337" class="Bound">x</a> <a id="2349" href="Agda.Builtin.Cubical.Path.html#381" data-type="A → A → Type ℓ" class="Function Operator">≡</a> <a id="2351" href="1Lab.Counterexamples.IsIso.html#2337" class="Bound">x</a><a id="2352" class="Symbol">)</a> <a id="2354" class="Symbol">→</a> <a id="2356" href="1Lab.Type.html#1336" data-type="Type" class="Datatype">⊥</a>
  <a id="2360" href="1Lab.Counterexamples.IsIso.html#2310" data-type="(iso-is-prop : is-prop (is-iso f)) → is-prop ((x : S¹) → x ≡ x) → ⊥" class="Function">¬is-prop-loops</a> <a id="2375" href="1Lab.Counterexamples.IsIso.html#2375" class="Bound">prop</a> <a id="2380" class="Symbol">=</a> <a id="2382" href="1Lab.HIT.S1.html#1873" data-type="refl ≡ loop → ⊥" class="Function">refl≠loop</a> <a id="2392" class="Symbol">(</a><a id="2393" href="1Lab.Path.html#58127" data-type="f ≡ g → (x : A) → f x ≡ g x" class="Function">happly</a> <a id="2400" class="Symbol">(</a><a id="2401" href="1Lab.Counterexamples.IsIso.html#2375" class="Bound">prop</a> <a id="2406" class="Symbol">(λ</a> <a id="2409" href="1Lab.Counterexamples.IsIso.html#2409" class="Bound">x</a> <a id="2411" class="Symbol">→</a> <a id="2413" href="1Lab.Path.html#3593" data-type="x ≡ x" class="Function">refl</a><a id="2417" class="Symbol">)</a> <a id="2419" href="1Lab.HIT.S1.html#2531" data-type="(x : S¹) → x ≡ x" class="Function">always-loop</a><a id="2430" class="Symbol">)</a> <a id="2432" href="1Lab.HIT.S1.html#640" data-type="S¹" class="InductiveConstructor">base</a><a id="2436" class="Symbol">)</a>
</pre>
<p>Hence, a contradiction:</p>
<pre class="Agda">  <a id="2478" href="1Lab.Counterexamples.IsIso.html#2478" data-type="(iso-is-prop : is-prop (is-iso f)) → ⊥" class="Function">contra</a> <a id="2485" class="Symbol">:</a> <a id="2487" href="1Lab.Type.html#1336" data-type="Type" class="Datatype">⊥</a>
  <a id="2491" href="1Lab.Counterexamples.IsIso.html#2478" data-type="(iso-is-prop : is-prop (is-iso f)) → ⊥" class="Function">contra</a> <a id="2498" class="Symbol">=</a> <a id="2500" href="1Lab.Counterexamples.IsIso.html#2310" data-type="(iso-is-prop : is-prop (is-iso f)) → is-prop ((x : S¹) → x ≡ x) → ⊥" class="Function">¬is-prop-loops</a> <a id="2515" href="1Lab.Counterexamples.IsIso.html#1913" data-type="is-prop (is-iso f) → is-prop ((x : A) → x ≡ x)" class="Function">is-prop-loops</a>
</pre>
  </article>
</div>
</main>
</body>
<style>.equations { display: none !important; }</style></html>
